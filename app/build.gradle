buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.github.triplet.gradle:play-publisher:2.7.5'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.diffplug.gradle.spotless'
apply plugin: 'com.github.triplet.play'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'

apply from: 'jacoco.gradle'

repositories {
    maven { url 'https://jitpack.io' }
}

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"
    ndkVersion '21.3.6528147'

    defaultConfig {
        applicationId "tech.relaycorp.courier"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "0.1"

        testInstrumentationRunner 'tech.relaycorp.courier.test.TestAndroidJUnitRunner'

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
                arg("room.incremental", "true")
            }
        }
    }

    signingConfigs {
        debug {}

        if (project.hasProperty('signingKeystorePath')) {
            release {
                storeFile file(signingKeystorePath)
                storePassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
                keyAlias System.getenv("ANDROID_KEYSTORE_KEY_ALIAS")
                keyPassword System.getenv("ANDROID_KEYSTORE_KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (project.hasProperty('signingKeystorePath')) {
                signingConfig signingConfigs.release
            }
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/*'
    }
    lintOptions {
        checkAllWarnings true
        warningsAsErrors true
        abortOnError true
        lintConfig file('lint.xml')
    }
    sourceSets {
        androidTest {
            java.srcDirs += "src/sharedTest/java"
            resources.srcDirs += "src/sharedTest/resources"
        }
        test {
            java.srcDirs += "src/sharedTest/java"
            resources.srcDirs += "src/sharedTest/resources"
        }
    }
}

dependencies {
    // Java 8
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.9'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"

    // Android
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'com.google.android.material:material:1.2.0-beta01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0'

    // Dependency Injection
    api 'com.google.dagger:dagger:2.28'
    kapt 'com.google.dagger:dagger-compiler:2.28'

    // Relaynet
    implementation 'tech.relaycorp:relaynet:1.15.6'
    implementation 'tech.relaycorp:relaynet-cogrpc:1.1.0'

    // ORM
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Preferences
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.github.tfcporciuncula:flow-preferences:1.1.0'

    // gRPC
    implementation "io.grpc:grpc-netty:$grpcVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"

    // Android TLS support for Netty
    implementation "io.netty:netty-handler:$nettyVersion"
    implementation 'org.conscrypt:conscrypt-android:2.4.0'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.65'

    // Display open-source licenses
    implementation "com.mikepenz:aboutlibraries:$aboutLibsVersion"

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutinesVersion"
    testImplementation 'org.mockito:mockito-inline:3.3.3'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation "io.grpc:grpc-testing:$grpcVersion"

    // Instrumentation Testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutinesVersion"
    androidTestImplementation "io.grpc:grpc-testing:$grpcVersion"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation('com.schibsted.spain:barista:3.5.0') {
        exclude group: 'org.jetbrains.kotlin'
    }
    kaptAndroidTest 'com.google.dagger:dagger-compiler:2.28'
}

spotless {
    kotlin {
        ktlint('0.36.0').userData(['disabled_rules': 'import-ordering'])
        target '**/*.kt'
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += [
            '-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi',
            '-Xuse-experimental=kotlinx.coroutines.FlowPreview',
            '-Xuse-experimental=kotlin.time.ExperimentalTime'
    ]
}

play {
    enabled = project.findProperty("enableGpp") == "true"
    track = "beta"
    serviceAccountCredentials = file(gcpServiceAccountCredentials)
    defaultToAppBundles = true
    resolutionStrategy = "auto"

    outputProcessor {
        versionNameOverride = versionName
    }
}
