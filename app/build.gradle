apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.diffplug.gradle.spotless'

apply from: 'jacoco.gradle'

repositories {
    maven { url "https://dl.bintray.com/relaycorp/maven" }
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "tech.relaycorp.courier"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
                arg("room.incremental", "true")
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        exclude 'META-INF/*'
    }
    lintOptions {
        checkAllWarnings true
        warningsAsErrors true
        abortOnError true
        lintConfig file('lint.xml')
    }
    sourceSets {
        androidTest {
            java.srcDirs += "src/sharedTest/java"
            resources.srcDirs += "src/sharedTest/resources"
        }
        test {
            java.srcDirs += "src/sharedTest/java"
            resources.srcDirs += "src/sharedTest/resources"
        }
    }
}

dependencies {
    implementation project(':relaynet-jvm') // TODO: remove when real version is finished
    implementation project(':relaynet-cogrpc-jvm')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"

    // Android
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'com.google.android.material:material:1.2.0-alpha06'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0'

    // Dependency Injection
    api 'com.google.dagger:dagger:2.27'
    kapt 'com.google.dagger:dagger-compiler:2.27'

    // Relaynet
    implementation 'tech.relaycorp:relaynet:1.11.1'

    // ORM
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Preferences
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.github.tfcporciuncula:flow-preferences:1.1.0'

    // gRPC
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"

    // Android TLS support for Netty
    implementation 'io.netty:netty-handler:4.1.48.Final'
    implementation 'org.conscrypt:conscrypt-android:2.4.0'

    // Base64 encoding
    // Android comes bundled with 1.3 (but hidden), so we're stuck with this version
    //noinspection GradleDependency
    api 'commons-codec:commons-codec:1.3'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutinesVersion"
    testImplementation 'org.mockito:mockito-inline:3.3.3'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'

    // Instrumentation Testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutinesVersion"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation('com.schibsted.spain:barista:3.2.0') {
        exclude group: 'org.jetbrains.kotlin'
    }
}

spotless {
    kotlin {
        ktlint('0.36.0').userData(['disabled_rules': 'import-ordering'])
        target '**/*.kt'
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += [
            '-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi',
            '-Xuse-experimental=kotlinx.coroutines.FlowPreview',
            '-Xuse-experimental=kotlin.time.ExperimentalTime'
    ]
}
